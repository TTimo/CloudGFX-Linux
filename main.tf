provider "aws" {
  region = "us-east-1"
}

variable "generated_key_name" {
  type        = string
  default     = "terraform-key-pair"
  description = "Key-pair generated by Terraform"
}

resource "tls_private_key" "terraform_key" {
  algorithm = "RSA"
  rsa_bits  = 4096
}

resource "aws_key_pair" "generated_key" {
  key_name   = var.generated_key_name
  public_key = tls_private_key.terraform_key.public_key_openssh

  # Generate "terraform-key-pair.pem" in current directory
  provisioner "local-exec" {
    command = <<-EOT
      echo '${tls_private_key.terraform_key.private_key_pem}' > ./'${var.generated_key_name}'.pem
      chmod 400 ./'${var.generated_key_name}'.pem
    EOT
  }

}

resource "aws_security_group" "cloudgfx_instance" {
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 5900
    to_port     = 5900
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = -1
    to_port     = -1
    protocol    = "icmp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port        = 0
    to_port          = 0
    protocol         = "-1"
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }
}

# Used for both the user password and the vnc password
resource "random_password" "fedora_password" {
  length = 8
}

resource "aws_instance" "cloudgfx_instance" {
  # Fedora-Cloud-Base-38-1.6.x86_64-hvm-us-east-1-gp2-0 Community AMI
  ami           = "ami-01752495da7056fa9"
  instance_type = "g4ad.xlarge"

  vpc_security_group_ids = [aws_security_group.cloudgfx_instance.id]

  root_block_device {
    volume_size = 50
  }

  tags = {
    Name = "Terraform CloudGFX-Linux"
  }

  key_name = aws_key_pair.generated_key.key_name

  connection {
    type = "ssh"
    host = self.public_ip
    user = "fedora"
    private_key = tls_private_key.terraform_key.private_key_pem
  }

  provisioner "file" {
    source      = "${path.root}/salt"
    destination = "/tmp/salt"
  }

  provisioner "file" {
    content     = "${random_password.fedora_password.result}"
    destination = "/tmp/password"
  }

  provisioner "remote-exec" {
    inline = [
      # Install and bootstrap salt
      "curl -L https://bootstrap.saltstack.com | sudo sh",
      # Remove directory /srv/salt to ensure it is clear
      "sudo rm -rf /srv/salt",
      # Move state tree from temporary directory to /srv/salt
      "sudo mv /tmp/salt /srv/salt",
      # Run salt-call
      "sudo salt-call --local  state.highstate --file-root=/srv/salt --pillar-root=/srv/pillar --retcode-passthrough -l info"
    ]
  }
}
